# Flow logs for the prod VPC, so there is a record of what moved inside the
# workload account independent of the hub's inspection logs.

resource "aws_cloudwatch_log_group" "vpc_flow" {
  #checkov:skip=CKV_AWS_338:14-day retention for a demo; a year is a production setting.
  #checkov:skip=CKV_AWS_158:CloudWatch CMK encryption is the production upgrade; demo uses the default key.
  name              = "/vpc/prod-flow"
  retention_in_days = 14
}

data "aws_iam_policy_document" "flow_assume" {
  statement {
    actions = ["sts:AssumeRole"]
    principals {
      type        = "Service"
      identifiers = ["vpc-flow-logs.amazonaws.com"]
    }
  }
}

resource "aws_iam_role" "flow" {
  name               = "prod-vpc-flow-logs"
  assume_role_policy = data.aws_iam_policy_document.flow_assume.json
}

data "aws_iam_policy_document" "flow_write" {
  statement {
    actions = [
      "logs:CreateLogStream",
      "logs:PutLogEvents",
      "logs:DescribeLogGroups",
      "logs:DescribeLogStreams",
    ]
    resources = ["${aws_cloudwatch_log_group.vpc_flow.arn}:*"]
  }
}

resource "aws_iam_role_policy" "flow" {
  name   = "flow-logs-write"
  role   = aws_iam_role.flow.id
  policy = data.aws_iam_policy_document.flow_write.json
}

resource "aws_flow_log" "prod" {
  vpc_id          = aws_vpc.prod.id
  traffic_type    = "ALL"
  log_destination = aws_cloudwatch_log_group.vpc_flow.arn
  iam_role_arn    = aws_iam_role.flow.arn
}
